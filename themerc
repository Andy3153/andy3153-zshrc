## vim: set fenc=utf-8 ts=2 sw=0 sts=0 sr et si tw=0 fdm=marker fmr={{{,}}} ft=zsh:
##
## themerc by Andy3153
## created   26/10/20 ~ 15:24:04
## remade1   23/08/21 ~ 00:13:35
## modified1 25/10/22 ~ 12:40:25
## remade2   11/03/24 ~ 11:38:39
##

# {{{ Arrows theme
##
## created     26/10/20 ~ 17:03:28
## git support 26/08/21 ~ 20:28:31
## remade1     25/10/22 ~ 13:05:20
## remade2     12/03/24 ~ 15:32:56
##
## I should've used vcs_info. I'm lazy now.
##

themeArrows()
{
  # {{{ Fetch info for the `isRoot` prompt segment
  if isRoot
  then syms[root]="$char[root] "
  fi
  # }}}

  # {{{ Fetch info for the `isSSH` prompt segment
  if isSSH
  then syms[ssh]="$char[ssh]"
  fi
  # }}}

  # Run before running a command
  preexec()
  {
    # {{{ Fetch info for the `elapsedTime` prompt segment
    timePreExecTime
    # }}}
  }

  # Run before every prompt
  precmd()
  {
    # {{{ Fetch info for the `elapsedTime` prompt segment
    timeDisplayTime
    if [ -z "${cmdTime[time]}" ]
    then PE[elapsedTime]="$F[blk]$K[bblk]$char[arrowL]%f%k"
    else PE[elapsedTime]="$F[blk]$K[bblk]$char[arrowL]$F[wht]$K[blk] $char[time] $cmdTime[time] %f%k"
    fi
    # }}}

    # {{{ Fetch info for the `git` prompt segment
    getGitBranch

    if isGitTreeClean
    then col[gitStatus]="grn"
    else col[gitStatus]="ylw"
    fi

    if isGit
    then PE[git]="$K[$col[gitStatus]]$F[blk]$char[arrowR] $char[git] $git[branch] $K[bblk]$F[$col[gitStatus]]$char[arrowR]%f"
    else PE[git]="$F[blk]$K[bblk]$char[arrowR]%k"
    fi
    # }}}
  }

  # {{{ Prompt elements not auto-updated
  PE=(
    delimiterL  "$char[circleL]"
    delimiterR  "$char[circleR]"
    isRoot      "$syms[root]"
    isSSH       "$syms[ssh]"
    username    "%n@%m"
    indicator   "$F[bblk]$K[bwht]$char[arrowR]$F[bwht]$K[wht]$char[arrowR]$F[wht]%k$char[arrowR]%f%k "
    folder      "$F[bwht]$char[arrowL]$F[blk]$K[bwht] $char[folder] %(5~|%-1~/$char[dots]/%2~|%4~) %f%k"
    errorCode   "$F[bblk]$K[bwht]$char[arrowL]$F[wht]$K[bblk]%(?.. $char[error] %?%  )%f%k"
  )
  # }}}

  # {{{ Arrange prompt elements in the prompts
  PROMPT='$F[blk]$PE[delimiterL]$F[wht]$K[blk]$PE[isRoot]$PE[isSSH]$PE[username] $PE[git]$PE[indicator]%f%k'
  RPROMPT='$PE[folder]$PE[errorCode]$PE[elapsedTime]$F[blk]$PE[delimiterR]%f%k'
  # }}}
}
# }}}
